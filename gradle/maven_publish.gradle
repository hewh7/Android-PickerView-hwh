apply plugin: 'maven-publish'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def REPOSITORY_URL = properties.getProperty('RELEASE_REPOSITORY_URL', "")
def SNAPSHOT_REPOSITORY_URL = properties.getProperty('SNAPSHOT_REPOSITORY_URL', "")
def USERNAME = properties.getProperty('NEXUS_USERNAME', "")
def PASSWORD = properties.getProperty('NEXUS_PASSWORD', "")
def GROUP_ID = properties.getProperty('GROUP_ID', "")

task packageSourcesJar(type: Jar) {
    classifier 'sources'
    from android.sourceSets.main.java.source
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId GROUP_ID
                artifactId POM_ARTIFACT_ID
                version VERSION_NAME
                artifact packageSourcesJar

                from(components["release"])

                pom {
                    description = 'Android-PickerView'
                    // If your project has a dedicated site, use its URL here
                    url = 'github.com/weinierfei/Android-PickerView'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    // Version control info, if you're using GitHub, follow the format as seen here
                    scm {
                        connection = 'scm:git:git://github.com/weinierfei/Android-PickerView.git'
                        developerConnection = 'scm:git:ssh://github.com/weinierfei/Android-PickerView.git'
                        url = 'https://github.com/weinierfei/Android-PickerView'
                    }
                }
            }
        }
        repositories {
            // The repository to publish to, Sonatype/MavenCentral
            maven {
                url = REPOSITORY_URL

                credentials {
                    username USERNAME
                    password PASSWORD
                }
            }
        }
    }
}
